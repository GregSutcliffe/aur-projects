# Puppet: Installer: Arch
# Maintainer : Greg Sutcliffe <puppet-aur (at) emeraldreverie (dot) org>
# Contributor: Hyacinthe Cartiaux <hyacinthe (dot) cartiaux (at) free (dot) fr>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail (dot) com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Miah Johnson <miah (at) chia-pet dot org>
# Contributor: Dave Simons <miouhpi (at) Gmail (dot) com>
# Contributor: Niels Abspoel <aboe76 (at) Gmail (dot) com>

pkgname=puppet3
pkgver=3.0.0rc3
pkgrel=1
pkgdesc="A system for automating system administration tasks."
arch=("any")
url="http://puppetlabs.com/projects/puppet/"
license=("APACHE")
depends=("ruby" "ruby-shadow" "facter")
backup=("etc/puppet/puppet.conf")
options=(emptydirs)
provides=("puppet")
conflicts=("puppet")
install="puppet.install"
source=("http://puppetlabs.com/downloads/puppet/puppet-$pkgver.tar.gz"
        "http://downloads.puppetlabs.com/hiera/hiera-puppet-1.0.0rc3.tar.gz"
        "http://downloads.puppetlabs.com/hiera/hiera-1.0.0rc4.tar.gz"
        "fileserver.conf"
        "puppet"
        "puppet.conf"
        "puppetagent.service"
        "puppetmaster"
        "puppetmaster.service")
md5sums=('0fa0e52c516c2c0c7e31f4e7bfa967b8'
         '6829fb5735e179b90b0491971d9b46a8'
         '175b7dfd864d003b81c8360eec5fc731'
         'ffacd9bfb9c7656a3a345dbc5eb0e67a'
         '59dbf39e251bc4877e7604a5876c642d'
         '65e7af811ce18f7c764f2ab5d2876d76'
         'bbfe0b847f08012cd534d96fe3758b3e'
         '4c507c580cb28d7a5adc9ea6b3626657'
         '15037c609ec10a6046387c43d1641464')

package() {
  cd $srcdir/puppet-$pkgver

  # Install
  ruby ./install.rb \
    --destdir=$pkgdir \
    --bindir=/usr/bin \
    --sbindir=/usr/sbin \
    --mandir=/usr/share/man

  # Hiera is essential for Puppet3
  install -d $pkgdir/usr/lib/ruby/site_ruby/1.9.1/lib/
  cp -r $srcdir/hiera-1.0.0rc4/lib/* $pkgdir/usr/lib/ruby/site_ruby/1.9.1/
  cp -r $srcdir/hiera-puppet-1.0.0rc3/lib/* $pkgdir/usr/lib/ruby/site_ruby/1.9.1/

  # Set up vim and emacs
  install -Dp -m0644 ext/vim/ftdetect/puppet.vim $pkgdir/usr/share/vim/vimfiles/ftdetect/puppet.vim
  install -Dp -m0644 ext/vim/syntax/puppet.vim $pkgdir/usr/share/vim/vimfiles/syntax/puppet.vim
  install -Dp -m0644 ext/emacs/puppet-mode.el $pkgdir/usr/share/emacs/site-lisp/puppet-mode.el
  install -Dp -m0644 ext/emacs/puppet-mode-init.el $pkgdir/usr/share/emacs/site-lisp/site-start.d/puppet-mode-init.el

  # Configuration and rc scripts
  install -d $pkgdir/etc/{puppet,rc.d}
  install -m 644 $srcdir/puppet.conf $pkgdir/etc/puppet/puppet.conf
  install -m 644 $srcdir/fileserver.conf $pkgdir/etc/puppet/fileserver.conf

  # Systemd files
  install -Dm 644 $srcdir/puppetagent.service $pkgdir/usr/lib/systemd/system/puppetagent.service
  install -Dm 644 $srcdir/puppetmaster.service $pkgdir/usr/lib/systemd/system/puppetmaster.service

  # Create puppet homedir
  install -d $pkgdir/var/lib/puppet

  install -D $srcdir/puppet $pkgdir/etc/rc.d/puppet
  install -D $srcdir/puppetmaster $pkgdir/etc/rc.d/puppetmaster

  # Copy the LICENSE file in the usual directory
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

}
