# Puppet: Installer: Arch
# Maintainer : Greg Sutcliffe <puppet-aur (at) emeraldreverie (dot) org>
# Contributor: Hyacinthe Cartiaux <hyacinthe (dot) cartiaux (at) free (dot) fr>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail (dot) com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Miah Johnson <miah (at) chia-pet dot org>
# Contributor: Dave Simons <miouhpi (at) Gmail (dot) com>
# Contributor: Niels Abspoel <aboe76 (at) Gmail (dot) com>

# RC style, reserved for later use
#pkgname=puppet3
#_rc=8
#_pkgver=3.0.0
#pkgver=${_pkgver}_rc${_rc}

pkgname=puppet3
pkgver=3.0.0
pkgrel=1
_hieraver=1.1.0
pkgdesc="A system for automating system administration tasks."
arch=("any")
url="http://puppetlabs.com/projects/puppet/"
license=("APACHE")
depends=("ruby" "ruby-shadow" "facter")
backup=("etc/puppet/puppet.conf")
options=(emptydirs)
provides=("puppet")
conflicts=("puppet")
install="puppet.install"
source=(http://puppetlabs.com/downloads/puppet/puppet-${pkgver//_/-}.tar.gz
        http://downloads.puppetlabs.com/hiera/hiera-puppet-1.0.0rc3.tar.gz
        http://downloads.puppetlabs.com/hiera/hiera-${_hieraver}.tar.gz
        http://downloads.puppetlabs.com/hiera/hiera-json-0.4.0.tgz
        fileserver.conf
        puppet
        puppet.conf
        puppetmaster
        hiera.yaml)
md5sums=('e3cf262862caffc9f00b169a78205770'
         '6829fb5735e179b90b0491971d9b46a8'
         '0a0774b508fddcc988eef92cd802cf7c'
         '389be2d95502d41d26da330207a33db3'
         'ffacd9bfb9c7656a3a345dbc5eb0e67a'
         '59dbf39e251bc4877e7604a5876c642d'
         '65e7af811ce18f7c764f2ab5d2876d76'
         '4c507c580cb28d7a5adc9ea6b3626657'
         'c2f380cdcffab3956fe9f6e261d6ea32')

package() {
  cd $srcdir/puppet-${pkgver//_/-}

  # Install
  ruby ./install.rb \
    --destdir=$pkgdir \
    --bindir=/usr/bin \
    --sbindir=/usr/sbin \
    --mandir=/usr/share/man

  # Hiera is essential for Puppet3
  install -d $pkgdir/usr/lib/ruby/site_ruby/1.9.1/lib/
  cp -r $srcdir/hiera-${_hieraver}/lib/* $pkgdir/usr/lib/ruby/site_ruby/1.9.1/
  cp -r $srcdir/hiera-json-0.4.0/lib/* $pkgdir/usr/lib/ruby/site_ruby/1.9.1/
  cp -r $srcdir/hiera-json-0.4.0/spec* $pkgdir/usr/lib/ruby/site_ruby/1.9.1/hiera/backend/
  cp -r $srcdir/hiera-puppet-1.0.0rc3/lib/* $pkgdir/usr/lib/ruby/site_ruby/1.9.1/
  install -d $pkgdir/usr/bin/
  install -Dp -m0755 $srcdir/hiera-${_hieraver}/bin/* $pkgdir/usr/bin/
  install -Dp -m0755 $srcdir/hiera-puppet-1.0.0rc3/bin/* $pkgdir/usr/bin/

  # Set up vim and emacs
  install -Dp -m0644 ext/vim/ftdetect/puppet.vim $pkgdir/usr/share/vim/vimfiles/ftdetect/puppet.vim
  install -Dp -m0644 ext/vim/syntax/puppet.vim $pkgdir/usr/share/vim/vimfiles/syntax/puppet.vim
  install -Dp -m0644 ext/emacs/puppet-mode.el $pkgdir/usr/share/emacs/site-lisp/puppet-mode.el
  install -Dp -m0644 ext/emacs/puppet-mode-init.el $pkgdir/usr/share/emacs/site-lisp/site-start.d/puppet-mode-init.el

  # Configuration and rc scripts
  install -d $pkgdir/etc/{puppet,rc.d}
  install -m 644 $srcdir/puppet.conf $pkgdir/etc/puppet/puppet.conf
  install -m 644 $srcdir/fileserver.conf $pkgdir/etc/puppet/fileserver.conf

  # Configuration for hiera / symlink is there to use hiera within puppet.
  install -m 644 $srcdir/hiera.yaml $pkgdir/etc/
  ln -snf /etc/hiera.yaml ${pkgdir}/etc/puppet/hiera.yaml

  # Systemd files
  install -Dm 644 $srcdir/puppet-${pkgver//_/-}/ext/systemd/puppetagent.service $pkgdir/usr/lib/systemd/system/puppetagent.service
  install -Dm 644 $srcdir/puppet-${pkgver//_/-}/ext/systemd/puppetmaster.service $pkgdir/usr/lib/systemd/system/puppetmaster.service

  # Create puppet homedir and hieradir
  install -d $pkgdir/var/lib/puppet
  install -d $pkgdir/var/lib/hiera

  install -D $srcdir/puppet $pkgdir/etc/rc.d/puppet
  install -D $srcdir/puppetmaster $pkgdir/etc/rc.d/puppetmaster

  # Copy the LICENSE file in the usual directory
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

}
