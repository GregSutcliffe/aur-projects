# Maintainer: Greg Sutcliffe <greg.sutcliffe@gmail.com>>

pkgname=foreman
pkgver=1.1
pkgrel=1
pkgdesc="Infrastruce management app built on Ruby-on_rails. Integrates with Puppet for complete lifecycle management or real or virtual servers"
arch=('any')
url="http://theforeman.org"
license=('GPL3')
depends=('ruby' 'ruby-bundler')
conflicts=('foreman-git')
backup=("etc/foreman/settings.yaml")
options=(emptydirs)
install="foreman.install"
source=(https://github.com/theforeman/foreman/archive/${pkgver}.tar.gz
        foreman.systemd
        foreman.tmpfiles.conf)
noextract=()
md5sums=('defdfd2daec51abca4aee4ff6c68199c'
         'c99762a8f6a0594965f54dcab5cf726a'
         'f6d26c35bf3b9a7c71105e72053785a1')

package() {
  cd $srcdir/foreman-${pkgver}

  # Main codebase
  install -d -m0755 $pkgdir/usr/share/foreman/
  cp -r ./ $pkgdir/usr/share/foreman/

  # Symlink config file to etc
  install -Dp -m0644 config/settings.yaml.example $pkgdir/etc/foreman/settings.yaml
  ln -sv /etc/foreman/settings.yaml $pkgdir/usr/share/foreman/config/settings.yaml
  install -Dp -m0644 config/database.yaml.example $pkgdir/etc/foreman/database.yaml
  ln -sv /etc/foreman/database.yaml $pkgdir/usr/share/foreman/config/database.yaml

  # logdirs
  install -d -m0755 $pkgdir/usr/share/foreman/logs
  install -d -m0755 $pkgdir/var/log/foreman

  # systemd
  install -Dm 644 $srcdir/foreman.systemd       $pkgdir/usr/lib/systemd/system/foreman.service
  install -Dm 644 $srcdir/foreman.tmpfiles.conf $pkgdir/usr/lib/tmpfiles.d/foreman.conf

}

# vim:set ts=2 sw=2 et:
